apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

ext.pocEndpoint = ext.has("pocEndpoint") ? ext.pocEndpoint : "not-set"

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField "String", "API_BASE_URL", "\"${pocEndpoint}\""
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.all {
            // https://github.com/robolectric/robolectric/issues/5115
            systemProperty("javax.net.ssl.trustStoreType", "JKS")

            // https://github.com/robolectric/robolectric/issues/5456
            systemProperty("robolectric.dependency.repo.id", "central")
            systemProperty("robolectric.dependency.repo.url", "https://repo1.maven.org/maven2")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    api 'com.squareup.retrofit2:retrofit:2.8.1'
    api 'com.squareup.okhttp3:okhttp:4.7.2'

    implementation 'com.squareup.retrofit2:converter-moshi:2.8.1'
    implementation ('com.squareup.moshi:moshi-kotlin:1.9.2') {
        exclude group: "org.jetbrains.kotlin", module: "kotlin-reflect"
    }

    implementation 'com.squareup.okhttp3:logging-interceptor:4.7.2'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.9.2'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation("com.squareup.okhttp3:mockwebserver:4.7.2")
    testImplementation 'androidx.test:core:1.2.0'
}
