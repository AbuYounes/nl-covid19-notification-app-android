apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "kotlin-kapt"
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: "com.github.triplet.play"
apply plugin: "com.osacky.fladle"
apply plugin: 'com.google.firebase.appdistribution'

version = "0.4.1"
def buildVersionCode = System.getenv("BUILD_ID") != null ? System.getenv("BUILD_ID").toInteger() : 1
archivesBaseName = "covid-notificatie-v${version}-${buildVersionCode}"

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "nl.rijksoverheid.en"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode buildVersionCode
        versionName version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "nl", "en", "de", "pl", "ro", "tr", "fr", "es", "bg", "ar"

        buildConfigField "boolean", "FEATURE_LOGGING", "false"
        buildConfigField "boolean", "FEATURE_DEBUG_NOTIFICATION", "false"
        buildConfigField "boolean", "FEATURE_DISABLE_UPLOAD", "false"
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.all {
            // https://github.com/robolectric/robolectric/issues/5115
            systemProperty("javax.net.ssl.trustStoreType", "JKS")

            // https://github.com/robolectric/robolectric/issues/5456
            systemProperty("robolectric.dependency.repo.id", "central")
            systemProperty("robolectric.dependency.repo.url", "https://repo1.maven.org/maven2")
        }
    }

    packagingOptions {
        exclude 'kotlin/**'
        exclude '**/*.kotlin_metadata'
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/*.version'
        exclude 'DebugProbesKt.bin'
        exclude 'build-data.properties'
        exclude 'play-**.properties'
    }

    signingConfigs {
        release {
            storeFile new File(System.getenv("KEYSTORE_FILE") != null ? System.getenv("KEYSTORE_FILE") : "dummy-keystore.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_KEY_ALIAS")
            keyPassword System.getenv("KEYSTORE_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "FEATURE_LOGGING", "true"
        }
    }

    flavorDimensions "environment"

    productFlavors {
        dev {
            dimension "environment"
            buildConfigField "boolean", "FEATURE_DEBUG_NOTIFICATION", "true"
            versionNameSuffix "-dev"
        }
        tst {
            dimension "environment"
            buildConfigField "boolean", "FEATURE_LOGGING", "true"
            buildConfigField "boolean", "FEATURE_DEBUG_NOTIFICATION", "true"
            versionNameSuffix "-tst"
        }
        acc {
            dimension "environment"
            buildConfigField "boolean", "FEATURE_LOGGING", "true"
            versionNameSuffix "-acc"
        }
        prod {
            dimension "environment"
            buildConfigField "boolean", "FEATURE_DISABLE_UPLOAD", "true"
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        coreLibraryDesugaringEnabled true
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if(variant.buildType.name == 'debug' && names.contains("prod")) {
            setIgnore(true)
        }
    }

    firebaseAppDistribution {
        appId="1:937920499570:android:9ac7b5cf66523282f957eb"
        releaseNotesFile=rootProject.file("app/src/main/play/release-notes/nl-NL/internal.txt")
        groups="android-latest"
    }
}

play {
    enabled = System.getenv("PLAY_STORE_CREDENTIALS") != null
    serviceAccountCredentials = System.getenv("PLAY_STORE_CREDENTIALS") != null ? new File(System.getenv("PLAY_STORE_CREDENTIALS")) : null
    track = "internal"
}

fladle {
    projectId = "vws-coronamelder"
    variant = "devDebug"
    testShards = 1
    devices = [
            [ "model" : "Pixel2", "version" : "28"]
    ]
    testTargets = ["notAnnotation nl.rijksoverheid.en.screenshots.StoreScreenshots"]
}

repositories {
    maven {
        url 'https://dl.bintray.com/blipinsk/maven/'
        content {
            includeGroup "com.bartoszlipinski"
        }
    }
    maven {
        url 'https://dl.bintray.com/lisawray/maven/'
        content {
            includeGroup "com.xwray"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'
    implementation 'androidx.appcompat:appcompat:1.3.0-alpha01'
    implementation "androidx.activity:activity-ktx:$activity_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.browser:browser:1.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.security:security-crypto:1.0.0-rc02"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation "com.jakewharton.timber:timber:$timber_version"

    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.google.android.play:core:1.8.0'
    implementation 'com.google.android.play:core-ktx:1.8.0'
    implementation 'com.google.android.gms:play-services-base:17.3.0'
    implementation 'com.google.android.gms:play-services-basement:17.3.0'
    implementation 'com.google.android.gms:play-services-tasks:17.1.0'

    implementation "com.xwray:groupie:$groupie_version"
    implementation "com.xwray:groupie-viewbinding:$groupie_version"
    implementation "com.airbnb.android:lottie:$lottie_version"

    implementation project(":en-api")
    implementation project(":api")
    implementation project(":signing")

    testImplementation 'junit:junit:4.13'
    androidTestImplementation "org.mockito:mockito-android:3.4.4"
    androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation("com.squareup.okhttp3:mockwebserver:4.8.0")
    testImplementation project(":test-support")
    testImplementation "androidx.work:work-testing:$work_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
    androidTestImplementation 'com.microsoft.appcenter:espresso-test-extension:1.4'
    androidTestImplementation 'com.bartoszlipinski:disable-animations-rule:2.0.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'tools.fastlane:screengrab:2.0.0'
    androidTestImplementation 'com.vanniktech:junit4-android-integration-rules:0.3.0'

    debugImplementation "androidx.fragment:fragment-testing:$fragment_version"
    debugImplementation project(":test-support")
    androidTestDebugImplementation project(":test-support")
}
