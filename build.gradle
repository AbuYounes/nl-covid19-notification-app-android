// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.72'
    ext.lifecycle_version = "2.2.0"
    ext.timber_version = "4.7.1"
    ext.nav_version = "2.3.0"
    ext.fragment_version = "1.2.5"
    ext.activity_version = "1.1.0"
    ext.android_test_version = "1.3.0-rc01"
    ext.coroutines_version = "1.3.7"
    ext.groupie_version = "2.8.0"
    ext.lottie_version = "3.4.1"
    ext.work_version = "2.4.0"

    repositories {
        google {
            content {
                includeGroup "com.google"
                includeGroup "com.android"
                includeGroupByRegex "com.google\\..*"
                includeGroupByRegex "com.android\\..*"
                includeGroupByRegex "androidx\\..*"
            }
        }
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
            content {
                includeGroup "com.github.triplet.gradle"
            }
        }
        jcenter() {
            content {
                includeGroup "org.jetbrains.trove4j"
            }
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "com.osacky.flank.gradle:fladle:0.10.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.30.0"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        classpath "com.github.triplet.gradle:play-publisher:2.8.0"
        classpath 'com.google.firebase:firebase-appdistribution-gradle:2.0.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: 'gradle/spotless.gradle'

allprojects {
    repositories {
        google {
            content {
                includeGroup "com.google"
                includeGroup "com.android"
                includeGroupByRegex "com.google\\..*"
                includeGroupByRegex "com.android\\..*"
                includeGroupByRegex "androidx\\..*"
            }
        }
        mavenCentral()
        jcenter() {
            content {
                includeGroup "org.jetbrains.trove4j"
                includeGroup "com.microsoft.appcenter"
                includeGroup "com.jraska"
                includeGroup "tools.fastlane"
            }
        }
    }

    afterEvaluate {
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = "1.8"
                freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"]
            }
        }

        project.plugins.withId("com.android.base") {
            android {
                compileSdkVersion 29
                defaultConfig {
                    minSdkVersion 23
                    targetSdkVersion 29
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
            }
        }
    }
}
